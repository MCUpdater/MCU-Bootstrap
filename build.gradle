import org.apache.tools.ant.filters.*

plugins {
    id 'workspace.java-application-conventions'
}

version = bootstrapVersion
sourceCompatibility = 16

dependencies {
    implementation project(':MCU-DownloadLib')
    implementation 'org.apache.commons:commons-compress:1.21'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/config.properties'
        filter(ReplaceTokens, tokens: [bootstrapURL: bootstrapURL, distribution: distribution, defaultPack: defaultPack])
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/config.properties'
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    manifest {
        attributes "Main-Class": 'org.mcupdater.BootstrapForm'
    }
}

application {
    mainClass = 'org.mcupdater.BootstrapForm'
}

jar.setDependsOn([':MCU-DownloadLib:jar'])